# Description
Claim ownership of the contract below to complete this level.

Things that might help

- Solidity Remix IDE

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import '@openzeppelin/contracts/math/SafeMath.sol';

contract Fallout {
  
  using SafeMath for uint256;
  mapping (address => uint) allocations;
  address payable public owner;


  /* constructor */
  function Fal1out() public payable {
    owner = msg.sender;
    allocations[owner] = msg.value;
  }

  modifier onlyOwner {
	        require(
	            msg.sender == owner,
	            "caller is not the owner"
	        );
	        _;
	    }

  function allocate() public payable {
    allocations[msg.sender] = allocations[msg.sender].add(msg.value);
  }

  function sendAllocation(address payable allocator) public {
    require(allocations[allocator] > 0);
    allocator.transfer(allocations[allocator]);
  }

  function collectAllocations() public onlyOwner {
    msg.sender.transfer(address(this).balance);
  }

  function allocatorBalance(address allocator) public view returns (uint) {
    return allocations[allocator];
  }
}
```

# Notes

At first glance this contract does seem like it has no logic error and it's time time to get down to "real" hacking.

The first interesting thing I noticed was that the owner address has the default value of `0x0000000000000000000000000000000000000000` when trying to get it through the commad `await contract.owner()`

This did bug me for a while until I noticed that what I thought to be the contructor of the contract was actually miss-spelled as `Fal1out()` instead of `Fallout()`, this results in the function `Fal1out` being a public function that anyone can call and therefore becoming the owner of the contract when executing `owner = msg.sender;`.